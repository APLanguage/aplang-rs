program        → (macro_annotation+ "\n\n")? declaration* EOF ;

declaration    → class_decl | fun_decl | var_decl ;

visibility     → "prot" | "pub" | "pak" ;

class_decl     → macro_annotation* visibility? "class" IDENTIFIER ( "<" type ("," type)* ">")?
                 (":" type ("," type)*)? "{" macro_annotation* declaration* "}" ;
fun_decl       → macro_annotation* visibility? generics? type? function ;
var_decl       → macro_annotation* visibility? type IDENTIFIER ( "=" expression )? ";" ;

statement      → for_stmt  | if_stmt | return_stmt | while_stmt | do_while | block | exp_stmt ;
for_stmt       → "for" "(" ( var_decl | exp_stmt | ";" )? expression? ";" expression? ")" statement 
               | "for" "(" IDENTIFIER ":" expression ")" statement ;
if_stmt        → "if"  "(" expression  ")" statement ( "else" statement )? ;
return_stmt    → "return" expression? ";" ;
while_stmt     → "while" "(" expression ")" statement ;
do_while       → "do" block "while" "(" expression ")" ";" ;
exp_stmt       → expression ";" ;

expression     → assignment ;
assignment     → call ( "+" | "-" | "*" | "**" | "/" | "&" | "|" | "<<" | ">>" | ">>>" | "^" | "?")
                 "=" assignment | if_ns_expr ;
if_ns_expr     → logic_or "?:" expression                | if_expr       ;
if_expr        → logic_or "?"  expression ":" expression | logic_or      ;
logic_or       → logic_and   (   "||" logic_and                       )* ;
logic_and      → equality    (   "&&" equality                        )* ;
equality       → comparison  ( ( "!=" | "=="              ) comparison)* ;
comparison     → term        ( ( ">"  | ">=" | "<" | "<=" ) term      )* ;
term           → factor      ( ( "-"  | "+"               ) factor    )* ;
factor         → bit_op      ( ( "/"  | "*"  | "**"       ) bit_op    )* ;
bit_op         → unary_left  ( ( "|"  | "^"  | "&"        ) unary_left)* ;
unary_left     →               ( "!"  | "~"  ) unary_left | unary_right  ;
unary_right    → primary       ( "--" | "++" ) ( NUMBER | "(" expression ")" ) | call  ;
call           → primary       ( "(" arguments? ")" | "." IDENTIFIER | array_clause )* ;

primary        → LITERAL | "self" | "super" | IDENTIFIER | lambda | "(" expression ")" | block ;
               
function       → IDENTIFIER "(" parameters? ")" "{" declaration* expression? "}" ;
parameters     → IDENTIFIER ":" type ( "," IDENTIFIER ":" type)* ;
arguments      → expression ( "," expression )* ;
block          → "{" (var_decl | statement)* expression? "}" ;
type           → path generics ;
path           → IDENTIFIER ( "." IDENTIFIER)* ;
generics       → "<" type? ("," type)* ">" ;
lambda         → "(" IDENTIFIER? ("," IDENTIFIER)* ")" "->" expression ;
LITERAL        → "true" | "false" | NUMBER | STRING | CHAR
NUMBER         → "0"-"9" ("." "0"-"9")? ( PREFIX )

array_clause   → "[" expression "]" ;
macro_annotation → ("@" | "#") "[" path
                   ("(" ( IDENTIFIER ("=" LITERAL) ("," IDENTIFIER ("=" LITERAL))*
                        | IDENTIFIER ("," IDENTIFIER)* )
                    ")")? "]" ;
